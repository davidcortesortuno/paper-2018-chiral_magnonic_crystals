# MIF 2.2

# -----------------------------------------------------------------------------

set PI [expr {4 * atan(1.)}]
set MU0 [expr {4 * $PI * 1e-7}]

set LENGTH [expr {3e-6}]
set WIDTH [expr {200e-9}]
set THICKNESS [expr {3e-9}]
set DX [expr {2e-9}]
set DY [expr {2e-9}]
set DZ [expr {3e-9}]

set Ms [expr {0.658e6}]
set A [expr {11.1e-12}]
Parameter D [expr {3e-3}]

set BIAS_FIELD [expr {0.25}]
set EXCIT_FIELD [expr {0.025}]

# Periodicity
Parameter a 100
Parameter w 50
set w1 [expr {$w}]
set w2 [expr {$a - $w}]

set ALPHA [expr {0.01}]
set GAMMA [expr {2.21e5}]

# Integration
set TIME_STEP 0.5e-12
set N_STAGES 8000

# Initial state
Parameter INIT_STATE InitialMagnetisation.omf

# -----------------------------------------------------------------------------

SetOptions {
    basename SWDynamics
    vector_field_output_format "text %.17g"
    vector_field_output_meshtype irregular
    vector_field_output_format {text %.17g}
}

# Exponentially increasing damping at the edges along the x-axis.  Will
# increase alpha from its starting value `alpha` to 1 in a region less than
# `width` away from `xmin` or `xmax`, where `xmin` and `xmax` are the extremal
# x-coordinates of the mesh.
proc exponentialAlpha_y { alpha Ymin Ymax width x y z } {
    set xn [expr {$x * 1e9}]
    set yn [expr {$y * 1e9}]
    set ya [expr {$Ymin + $width}]
    set yb [expr {$Ymax - $width}]
    

    if {$yn <= $ya} {
        set exp_alpha [expr {exp(($Ymin - $yn) * log($alpha) / ($Ymin - $ya))}]
    } elseif {$yn >= $yb} {
        set exp_alpha [expr {exp(($Ymax - $yn) * log($alpha) / ($Ymax - $yb))}]
    } else {
        set exp_alpha [expr {$alpha}]
    }

    return $exp_alpha
}


proc periodicProc { p1 p2 x y z } {
    set xn [expr {$x * 1e9}]
    set yn [expr {$y * 1e9}]

    set xr [expr {fmod($xn, double($p1 + $p2))}]

    if {$xr < $p1 } {
       return 2
    } elseif { $xr >= $p1 && $xr <= ($p1 + $p2) } { 
       return 1
    } else {
        return 0
    }
}

Specify Oxs_ScriptAtlas:periodicAtlas [subst {
    xrange {0e-9 $LENGTH}
    yrange {0e-9 $WIDTH}
    zrange {0e-9 $THICKNESS}
    regions { nodmi dmi }
    script_args { rawpt }
    script { periodicProc $w1 $w2 }
}]

Specify Oxs_RectangularMesh:mesh [subst {
  cellsize {$DX $DY $DZ}
  atlas :periodicAtlas
}]

Specify Oxs_UniformExchange [subst {
  A  $A
}]

Specify Oxs_Demag {}

# Uniform DMI is used here
Specify Oxs_DMExchange6Ngbr:DMI [subst {
    default_D $D
    atlas :periodicAtlas
    D {
        nodmi nodmi 0
        dmi dmi $D
        nodmi dmi $D
  }
}]

# Time dependent excitation field ---------------------------------------------

Specify Oxs_StageZeeman [subst {
  script LocalFieldSpec
  stage_count $N_STAGES
}]

proc LocalFieldSpec { stage } {
  global TIME_STEP EXCIT_FIELD BIAS_FIELD MU0 PI 
  set spec Oxs_ScriptVectorField
  set time [expr {$stage * $TIME_STEP}]
  set f [expr {60.0e9}]
  set sinc_args [expr {2.0 * $PI * $f * ($time - 49.9999e-12)}]
  set hField [expr {$EXCIT_FIELD / $MU0}]
  set h_excit [expr {$hField * sin($sinc_args) / $sinc_args}]
  set biasH [expr {$BIAS_FIELD / $MU0}]

  lappend spec [subst {
    atlas :periodicAtlas
    script {LocalField $biasH $h_excit}
  }]

  return $spec
}

proc LocalField { biasH h_excit xrel yrel zrel } {
  global LENGTH WIDTH

  set x [expr {$xrel * $LENGTH - $LENGTH * 0.5}]
  set y [expr {$yrel * $WIDTH - $WIDTH * 0.5}]

    if {($x <= 2e-9) & ($x >= -2e-9) & ($y >= -80e-9) & ($y <= 80e-9) } {
        set sum [expr {$h_excit}]
    } else {
        set sum [expr {0}]
    }
    return [list $sum $biasH 0.0]
}

# -----------------------------------------------------------------------------

# exponentialAlpha: alpha Ymin Ymax width (units in nm)
Specify Oxs_ScriptScalarField:alpha [subst {
   script_args { rawpt }
   script {exponentialAlpha_y $ALPHA 0 [expr {$WIDTH * 1e9}] 20}
   atlas :periodicAtlas
}]

Specify Oxs_RungeKuttaEvolve [subst {
  do_precess 1
  gamma_LL $GAMMA
  method rkf54
  alpha :alpha
}]

# -----------------------------------------------------------------------------

Specify Oxs_TimeDriver [subst {
 evolver Oxs_RungeKuttaEvolve
 mesh :mesh
 stopping_time $TIME_STEP
 stage_count $N_STAGES
 Ms $Ms
 m0 { Oxs_FileVectorField {
   atlas :periodicAtlas
   norm  1
   file $INIT_STATE
} }
}]

Destination table mmArchive
Destination mags mmArchive

Schedule DataTable table Stage 1
Schedule Oxs_TimeDriver::Magnetization mags Stage 1
